module refactor

imports
  lib/refactor-common.generated
  include/SLL
  lib/editor-common.generated
  sll
  interpreter
  gen
  utils

rules

rename-function:
    (newname, selected-name, position, ast, path, project-path) -> ([(ast, new-ast)], fatal-errors, errors, warnings)
with
    new-ast  := <topdown(try(ren-func(|selected-name, newname)))> ast;
    (errors, warnings) := <semantic-constraint-issues> (ast, new-ast);
    fatal-errors := []

ren-func(|old-name, new-name): FDef(old-name, args*, body) -> FDef(new-name, args*, body)
ren-func(|old-name, new-name): GDef(old-name, pat, args*, body) -> GDef(new-name, pat, args*, body)
ren-func(|old-name, new-name): GDef(old-name, pat, body) -> FDef(new-name, pat, body)
ren-func(|old-name, new-name): FCall(old-name, body) -> FCall(new-name, body)

extract-function:
    (newname, selected-body, position, ast, path, project-path) -> ([(ast, new-ast)], fatal-errors, errors, warnings)
where
    new-def := <make-func-def(|newname)> selected-body;
    new-ast := <topdown(try(change-funcall(|selected-body, newname))); Program(id, insert-def(|new-def))> ast;
    (errors, warnings) := <semantic-constraint-issues> (ast, new-ast);
    prog-before := <collect-one(?MainDef(Expr(<id>))); deforest-program(rewrite-step-alt(|ast)|ast)> ast;
    reset-name-counter(|"f");
    reset-name-counter(|"g");
    reset-name-counter(|"v");
    prog-after := <collect-one(?MainDef(Expr(<id>))); deforest-program(rewrite-step-alt(|new-ast)|new-ast)> new-ast;
    // checking equality by transformation
    <eq> (prog-before, prog-after);
    fatal-errors := []

insert-def(|definition): lst -> <insert> (0, definition, lst)

change-funcall(|body, name): body -> FCall(name, <collect-all(?Var(_))> body)

make-func-def(|name): body -> FDef(name, <collect-all(?Var(_)); map(\ Var(n) -> DVar(n) \)> body, body)


semantic-constraint-issues:
    (ast, new-ast) -> (<diff>(new-errors, errors), <diff>(new-warnings, warnings))
where
    (_, errors, warnings, _) := <editor-analyze> (ast, "", "");
    (_, new-errors, new-warnings, _) := <editor-analyze> (new-ast, "", "")
