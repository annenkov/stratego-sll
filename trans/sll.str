module sll

imports
  libstratego-lib
  libstratego-gpp
  libstratego-aterm
  include/SLL
  lib/editor-common.generated
  lib/index-library.generated
  lib/analysis-library.generated
  lib/analysis-library-internal.generated
  check
  interpreter
  conftree
  refactor
  gen
  testhelpers
  dot

rules // Main editor interface (defined by editor/SLL-Builders and -References.esv)

  // Analyzes the current program, returning a tuple with errors, warnings, and notes;
  // each a list of (term, message) tuples or simply (message) terms.
  editor-analyze:
    (ast, path, project-path) -> (ast, errors, warnings, notes)
    with
      editor-init;
      analyze;
      errors   := <collect-all(constraint-error, conc)> ast;
      warnings := <collect-all(constraint-warning, conc)> ast;
      notes    := <collect-all(constraint-note, conc)> ast

  run-main:
    (selected, position, ast, path, project-path) -> (filename, <pp-sll-string> result)
    with
      filename := <guarantee-extension(|"result")> path;
      <collect-one(?MainDef(main-def))> selected;
      result   := <rewrite-main-def(|ast)> main-def

  trace-main:
    (selected, position, ast, path, project-path) -> (filename, <map(pp-sll-string)> trace)
    with
      filename := <guarantee-extension(|"result")> path;
      <collect-one(?MainDef(main-def))> selected;
      trace := <rewrite-trace-main-def(|ast)> main-def

  build-conf-tree:
      (selected, position, ast, path, project-path) -> (filename, <prettify-tree(pp-sll-string)> conf-tree)
    with
      filename := <guarantee-extension(|"aterm")> path;
      <collect-one(?MainDef(Expr(main-def)))> selected;
      conf-tree := <build-conf-tree(rewrite-small-step(|ast),id|ast)> main-def

  deforest-tree:
      (selected, position, ast, path, project-path) -> (filename, <prettify-tree(pp-sll-string)> conf-tree)
    with
      filename := <guarantee-extension(|"aterm")> path;
      <collect-one(?MainDef(Expr(main-def)))> selected;
      conf-tree := <build-conf-tree(rewrite-small-step(|ast),id|ast); simplify> main-def

 transform: (selected, position, ast, path, project-path) -> (filename, <pp-sll-string> prog)
  with
      filename := <concat-strings> [<remove-extension> path, ".trans.sll"];
      <collect-one(?MainDef(Expr(main-def)))> selected;
      prog := <transform-program(rewrite-small-step(|ast)|ast)> main-def

 deforest: (selected, position, ast, path, project-path) -> (filename, <pp-sll-string> prog)
  with
      filename := <concat-strings> [<remove-extension> path, ".trans.sll"];
      <collect-one(?MainDef(Expr(main-def)))> selected;
      prog := <deforest-program(rewrite-small-step(|ast)|ast)> main-def

 supercompile: (selected, position, ast, path, project-path) -> (filename, <pp-sll-string> prog)
  with
      filename := <concat-strings> [<remove-extension> path, ".trans.sll"];
      <collect-one(?MainDef(Expr(main-def)))> selected;
      prog := <supercompile-program(rewrite-small-step(|ast)|ast)> main-def


    // Prints the abstract syntax ATerm of a selection.
  generate-aterm:
    (selected, position, ast, path, project-path) -> (filename, result)
    with
      filename := <guarantee-extension(|"aterm")> path;
      result   := selected // we just return the input term

  // Resolves a reference when the user control-clicks or presses F3 in the editor.
  editor-resolve:
    (node, position, ast, path, project-path) -> target
    where
      target     := <GetFunc> node

  // Returns "hover help" information for a particular node in the editor.
  // For references, this rule is invoked using the resolved term.
  editor-hover:
    (target, position, ast, path, project-path) -> $[Hover help: [<write-to-string> target]]

  // Completes an identifier when the user presses control-space
  // (the completion identifier in the AST provides additional context information)
  editor-complete:
    (node, position, ast, path, project-path) ->  proposals
    where
        proposals := <all-keys-GetFunc>

  // DOT file generation
  generate-dot: (selected, position, ast, path, project-path) -> (filename, <dot-gen> conf-tree)
  with
      filename := <guarantee-extension(|"dot")> path;
      <collect-one(?MainDef(Expr(main-def)))> selected;
      conf-tree := <build-conf-tree(rewrite-small-step(|ast),id|ast)> main-def
  // some workarounds
  my-pp-sll-string: (t, _, _, _, _) -> <debug(!"pp: "); pp-sll-string> t
  my-pp-sll-string: t -> <debug(!"pp: "); pp-sll-string> t

  reconstruct-term: (old, new) -> <try(my-pp-sll-string)> new
